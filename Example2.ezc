easy_c 1.0

library Easy_C
library CV
library Math
library High_GUI2

routine main@Easy_C
    takes arguments Array[String]
    returns Integer

    call d@("Hello!\n\")

    if arguments.size < 2
	call d@("usage: Example2 avi_file_or_camera_digit\n\")
    else
	file_name :@= arguments[1]
	window_name :@= "Example2"
	call named_window@CV(window_name, window_auto_size@CV)
	capture :@= null@CV_Capture
	if file_name.size = 1 && is_decimal_digit@(file_name[0])
	    camera_number :@= integer@(decimal_convert@(file_name[0]))
	    capture := create_camera_capture@CV(capture_any@CV + camera_number)
	    call set_capture_property@(capture,
	      property_frame_width@CV_Capture, double@(640))
	    call set_capture_property@(capture,
	      property_frame_height@CV_Capture, double@(480))
	else
	    capture := create_file_capture@CV(file_name)
	while 1t
	    frame :@= query_frame@(capture)
	    if frame == null@CV_Image
		break
	    call show_image@CV(window_name, frame)
	    character :@= wait_key@CV(33i)
	    if character = 27i
		break
	call release_capture@(capture)
	call destroy_window@CV(window_name)

    return 1i

